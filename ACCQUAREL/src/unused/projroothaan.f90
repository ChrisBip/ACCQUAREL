SUBROUTINE PROJECTEDROOTHAAN(EIG,EIGVEC,NBAST,POEFM,PHI,TRSHLD,MAXITR,PPROJM)
! "Projected" Roothaan's algorithm
! Reference: C. C. J. Roothaan, New developments in molecular orbital theory, Rev. Modern Phys., 23(2), 69-89, 1951.
  USE case_parameters ; USE data_parameters ; USE basis_parameters ; USE common_functions ;
  USE matrices ; USE matrix_tools ; USE metric_relativistic ; USE scf_tools
  IMPLICIT NONE
  INTEGER,INTENT(IN) :: NBAST
  DOUBLE PRECISION,DIMENSION(NBAST),INTENT(INOUT) :: EIG
  DOUBLE COMPLEX,DIMENSION(NBAST,NBAST),INTENT(INOUT) :: EIGVEC
  DOUBLE COMPLEX,DIMENSION(NBAST*(NBAST+1)/2),INTENT(IN) :: POEFM
  TYPE(twospinor),DIMENSION(NBAST),INTENT(IN) :: PHI
  DOUBLE PRECISION,INTENT(IN) :: TRSHLD
  INTEGER,INTENT(IN) :: MAXITR
  DOUBLE COMPLEX,DIMENSION(NBAST*(NBAST+1)/2),OPTIONAL,INTENT(IN) :: PPROJM

  INTEGER :: ITER,LOON,INFO,I
  DOUBLE PRECISION :: ETOT,ETOT1
  DOUBLE COMPLEX,DIMENSION(:),ALLOCATABLE :: PTEFM,PPOEFM,PPTEFM,PDM,PDM1
  LOGICAL :: NUMCONV

! INITIALIZATION AND PRELIMINARIES
  ITER=0
  ETOT1=0.D0

  ALLOCATE(PDM(1:NBAST*(NBAST+1)/2),PDM1(1:NBAST*(NBAST+1)/2))
  ALLOCATE(PTEFM(1:NBAST*(NBAST+1)/2),PPOEFM(1:NBAST*(NBAST+1)/2),PPTEFM(1:NBAST*(NBAST+1)/2))

  CALL CHECKORB(EIG,NBAST,LOON)
  CALL FORMDM(PDM,EIGVEC,NBAST,LOON,LOON+NBE-1)

  PDM1=PDM
  PPOEFM=ABCBA(PS,PPROJM,POEFM,NBAST)
! LOOP
1 CONTINUE
  ITER=ITER+1
  WRITE(*,*)' '
  WRITE(*,*)'### SUBITER =',ITER

! Assembly and diagonalization of the projected Fock matrix
  CALL BUILDTEFM(PTEFM,NBAST,PHI,PDM)
  PPTEFM=ABCBA(PS,PPROJM,PTEFM,NBAST)   
  CALL EIGENSOLVER(PPOEFM+PPTEFM,PCFS,NBAST,EIG,EIGVEC,INFO)
  IF (INFO/=0) GO TO 4
! Assembly of the density matrix according to the aufbau principle
  CALL FORMDM(PDM,EIGVEC,NBAST,1,NBE)
! Numerical convergence check
  CALL BUILDTEFM(PTEFM,NBAST,PHI,PDM)
  CALL CHECKNUMCONV(PDM,PDM1,ABCBA(PS,PPROJM,POEFM+PTEFM,NBAST),NBAST,0.D0,0.D0,TRSHLD,NUMCONV)
  IF (NUMCONV) THEN
! Convergence reached
     GO TO 2
  ELSE IF (ITER==MAXITR) THEN
! Maximum number of iterations reached without convergence
     GO TO 3
  ELSE
! Convergence not reached, increment
     ETOT1=ETOT
     PDM1=PDM
     GO TO 1
  END IF

! MESSAGES

2 WRITE(*,*)' ' ; WRITE(*,*)'Subroutine PROJECTEDROOTHAAN: convergence after',ITER,'iterations.'
  OPEN(9,FILE='eigenvalues.txt',STATUS='UNKNOWN',ACTION='WRITE')
  DO I=1,NBAST
     WRITE(9,*)I,EIG(I)
  END DO
  CLOSE(9)
  GO TO 5
3 WRITE(*,*)' ' ; WRITE(*,*)'Subroutine PROJECTEDROOTHAAN: no convergence after',ITER,'iterations.'
  OPEN(9,FILE='eigenvalues.txt',STATUS='UNKNOWN',ACTION='WRITE')
  DO I=1,NBAST
     WRITE(9,*)I,EIG(I)
  END DO
  CLOSE(9)
  GO TO 5
4 WRITE(*,*)'(called from subroutine PROJECTEDROOTHAAN)'
5 DEALLOCATE(PDM,PDM1,PTEFM,PPOEFM,PPTEFM)
  RETURN
END SUBROUTINE
